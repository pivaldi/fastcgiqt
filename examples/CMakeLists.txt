cmake_minimum_required(VERSION 2.8)
find_package(Qt4)
include(${QT_USE_FILE}) #contains path to Qt header
SET(SUBDIRS
	BigWorld
	Dumper
	GetGreeter
	HelloWorld
	PostGreeter
	PostStreamExample
	PrefixExample
	ThreadedDumper
)

SET(COMPLEX_EXAMPLES)
IF(QT_QTWEBKIT_FOUND)
	LIST(APPEND COMPLEX_EXAMPLES EmbedExample)
ENDIF()

SET(DATA_FILES)

IF(WITH_SERVICE_SUPPORT)
	LIST(APPEND SUBDIRS ServiceExample)
ENDIF()
IF(WITH_XSLT_SUPPORT)
	LIST(APPEND SUBDIRS XsltExample)
	LIST(APPEND DATA_FILES XsltExample/example.xsl)
ENDIF()

### don't edit below here ###

INCLUDE_DIRECTORIES(../include)

MACRO(BUILD_EXAMPLE EXAMPLE_NAME)
	SET(MOC_SOURCES)
	SET(HEADERS)
	SET(SOURCES)
	FILE(GLOB HEADERS ${EXAMPLE_NAME}/*.h)
	FILE(GLOB SOURCES ${EXAMPLE_NAME}/*.cpp)
	QT4_WRAP_CPP(MOC_SOURCES ${HEADERS})
	ADD_EXECUTABLE(${EXAMPLE_NAME} ${SOURCES} ${MOC_SOURCES})
	TARGET_LINK_LIBRARIES(${EXAMPLE_NAME} ${QT_LIBRARIES} FastCgiQt0)
	SET_TARGET_PROPERTIES(${EXAMPLE_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${EXAMPLE_NAME})
ENDMACRO()

FOREACH(subdir ${SUBDIRS})
	BUILD_EXAMPLE(${subdir})
ENDFOREACH()

FOREACH(subdir ${COMPLEX_EXAMPLES})
	ADD_SUBDIRECTORY(${subdir})
ENDFOREACH()

FOREACH(DATA_FILE ${DATA_FILES})
	CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/${DATA_FILE} ${CMAKE_CURRENT_BINARY_DIR}/${DATA_FILE})
ENDFOREACH()
